{
  lib,
  pkgs,
  rustPlatform,
  fetchFromGitHub,
  namespace,
  ...
}:
let
  # Game data from the original freeware version of the game. All other
  # versions of the game are proprietary so we can't ship them. However,
  # you can dump game data yourself (see doukutsu-rs for instructions)
  # and use that instead for your personal use.
  gameData = fetchFromGitHub {
    owner = "doukutsu-rs";
    repo = "game-data";
    rev = "f08e35952f2d9358f6591984d5870e7312492999";
    sha256 = "0irk47ymcdq9i9zllx0m28yk9dw83zay07k3sn8iqjy2ikc64q52";
  };
in
rustPlatform.buildRustPackage rec {
  pname = "doukutsu-rs";
  version = "0.99.0-beta4";

  src = fetchFromGitHub {
    owner = pname;
    repo = pname;
    rev = version;
    sha256 = "CLSisgl7Ng2ucUbkWyOJ9kL3GqATGONgPSwFCE3Qxf0=";
  };

  cargoPatches = [
    # The project doesn't include a Cargo.lock file, so we need to patch one in.
    # This patch is generated by cloning the doukutsu-rs repository and running the following commands:
    #
    # cargo update
    # git diff --no-index --patch /dev/null ./Cargo.lock
    ./cargo.patch

    # The project tries to build SDL from source as well as use a static library as opposed to
    # the dynamic one that NixPkgs provides. This patch disables the "bundled" and "static-link" options.
    ./cargo-toml.patch
  ];

  cargoLock = {
    # As of 2023-07-19 we now *also* need to provide a lock file in addition to the patch above.
    lockFile = ./Cargo.lock;

    outputHashes = {
      "glutin-0.26.0" = "sha256-IBR2p9TijvFsUCycQMy4Y5idEXsm8bUVzjpCkyEXI1Q";
      "lua-ffi-0.1.2" = "sha256-xOjHc2hT6GqdyQoFBeCteGCXO6/8JAK49xuHNxU5PmY";
      "sdl2-0.35.2" = "sha256-wBwmz5aL8occNueLr74PVAYp/LLzL5z5SpDrvGbjaso";
      "winit-0.24.0" = "sha256-1iIUPGJoSisgKnATisd0EUV7uHyHC0G3aRviwO+PJdA";
    };
  };

  postInstall = ''
    mkdir -p $out/libexec/doukutsu-rs

    mv $out/bin/doukutsu-rs $out/libexec/doukutsu-rs/

    ln -s $out/libexec/doukutsu-rs/doukutsu-rs $out/bin/doukutsu-rs

    ln -s ${gameData} $out/libexec/doukutsu-rs/data
  '';

  # Tests currently fail in the project, so we're forced to skip them.
  doCheck = false;

  nativeBuildInputs = with pkgs; [
    pkg-config
    cmake
  ];

  buildInputs = with pkgs; [
    alsa-lib
    SDL2
  ];

  meta = {
    platforms = lib.platforms.linux;
  };
}
